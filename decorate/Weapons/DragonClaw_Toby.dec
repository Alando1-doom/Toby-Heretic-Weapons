ACTOR DragonClaw_Toby : Blaster
{
	Game Heretic
	Weapon.UpSound "weapons/dragonclaw_up"
	Inventory.PickupSound "weapons/dragonclaw_pkup"
	Weapon.AmmoType "BlasterAmmo"
	Weapon.SisterWeapon "DragonClawPowered_Toby"
	Decal RailScorch
	States
	{
	Spawn:
		WBLS A 1 A_SpawnItemEx("WeaponPickupChecker_DragonClaw", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0)
        WBLS AA 20
        Loop
	Ready:
		BLSR A 1 A_WeaponReady
		Loop
	Deselect:
		BLSR A 1 A_Lower
		Loop
	Select:
		BLSR A 1 A_Raise
		Loop
	Fire:
		BLSR BC 3 Bright
	Hold:
		BLSR D 2 Bright A_FireBlasterPL1
		BLSR CB 2 Bright
		BLSR A 0 A_ReFire
		Goto Ready
	}
}

ACTOR DragonClawPowered_Toby : BlasterPowered
{
	Game Heretic
	+WEAPON.POWERED_UP
	Weapon.UpSound "weapons/dragonclaw_up"
	Weapon.AmmoType "BlasterAmmo"
	Weapon.SisterWeapon "DragonClaw_Toby"
	Decal RailScorch
	States
	{
	Ready:
		BLSR EFG 3 A_WeaponReady
		Loop
	Fire:
		BLSR BC 0 Bright
	Hold:
		BLSR D 3 Bright A_FireCustomMissile("BlasterFX1")
		BLSR CB 4 Bright
		BLSR A 0 A_ReFire
		Goto Ready
	}
}

ACTOR DragonClaw_Giver : CustomInventory replaces Blaster
{
	Game Heretic
	SpawnID 28
    Inventory.PickupMessage "$TXT_WPNBLASTER"
	Inventory.RespawnTics 105
	Inventory.PickupSound ""
    States
    {
    Spawn:
        WBLS A 1 A_SpawnItemEx("WeaponPickupChecker_DragonClaw", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0)
        WBLS AA 20
        Loop
    Pickup:
		TNT1 A 1 A_JumpIfInventory("DragonClaw_Toby", 1, "HasDragonClaw")
        TNT1 A 1 A_GiveInventory("DragonClaw_Toby", 1)
		TNT1 A 1 A_PlaySoundEx("weapons/dragonclaw_pkup", "Auto", 0, -1)
        Stop
	HasDragonClaw:
		TNT1 A 1 A_JumpIfInventory("BagOfHolding_Toby", 1, "HasBP")
        TNT1 A 1 A_JumpIfInventory("BlasterAmmo", 200, "NoPickup")
        TNT1 A 1 A_GiveInventory("BlasterAmmo", 30)
		TNT1 A 1 A_PlaySoundEx("weapons/dragonclaw_pkup", "Auto", 0, -1)
		TNT1 A 1 A_PlaySoundEx("ann/dragonclaw", "Auto", 0, -1)
		Stop
    HasBP:
        TNT1 A 1 A_JumpIfInventory("BlasterAmmo", 400, "NoPickup")
        TNT1 A 1 A_GiveInventory("BlasterAmmo", 30)
		TNT1 A 1 A_PlaySoundEx("weapons/dragonclaw_pkup", "Auto", 0, -1)
		TNT1 A 1 A_PlaySoundEx("ann/dragonclaw", "Auto", 0, -1)
        Stop
    NoPickup:
        TNT1 A 1 A_PlaySoundEx("stat/full", "SoundSlot6", 0, 0)
        Fail
    }
}
