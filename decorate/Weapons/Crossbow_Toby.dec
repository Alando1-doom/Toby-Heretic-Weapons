ACTOR Crossbow_Toby : Crossbow
{
	Game Heretic
	Weapon.SlotNumber 3
	Weapon.YAdjust 0
	Weapon.UpSound "weapons/cbow_up"
	Inventory.PickupSound "weapons/cbow_pkup"
	AttackSound "weapons/bowshoot"
	Weapon.AmmoType "CrossbowAmmo"
	Weapon.SisterWeapon "CrossbowPowered_Toby"
	Decal CrossbowScorch
	States
	{
	Spawn:
		WBOW A 1 A_SpawnItemEx("WeaponPickupChecker_Crossbow", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0)
        WBOW AA 18
        Loop
	Ready:
		CRBW AAAABBBBCCCC 1 A_WeaponReady
		Loop
	Deselect:
		CRBW A 1 A_Lower
		Loop
	Select:
		CRBW A 1 A_Raise
		Loop
	Fire:
		CRBW D 1 Bright A_PlayWeaponSound("weapons/bowshoot")
		CRBW D 0 Bright A_FireCustomMissile("CBowShot1",0,1,0,0)
		CRBW D 0 Bright A_FireCustomMissile("CBowShot1",-2,0,0,0)
		CRBW D 0 Bright A_FireCustomMissile("CBowShot1",2,0,0,0)
		CRBW D 4 Bright
		CRBW EFGH 3
		CRBW AB 4
		CRBW C 5 A_ReFire
		Goto Ready
	}
}

ACTOR CrossbowPowered_Toby : Crossbow_Toby
{
	Game Heretic
	+WEAPON.POWERED_UP
	Weapon.AmmoGive 0
	Weapon.SisterWeapon "Crossbow_Toby"
	Tag "$TAG_CROSSBOWP"
	States
	{
	Ready:
		CRBW IIIIJJJJKKK 1 A_WeaponReady
		Loop
	Fire:
		CRBW D 1 Bright A_PlayWeaponSound("weapons/bowshoot")
		CRBW D 0 Bright A_FireCustomMissile("CBowShot2",0,1,0,0)
		CRBW D 0 Bright A_FireCustomMissile("CBowShot2",-1,0,0,0)
		CRBW D 0 Bright A_FireCustomMissile("CBowShot2",-2,0,0,0)
		CRBW D 0 Bright A_FireCustomMissile("CBowShot2",2,0,0,0)
		CRBW D 0 Bright A_FireCustomMissile("CBowShot2",1,0,0,0)
		CRBW D 4 Bright
		CRBW E 3
		CRBW F 2
		CRBW G 3
		CRBW H 2
		CRBW A 3
		CRBW B 3
		CRBW C 4 A_ReFire
		Goto Ready
	}
}


ACTOR Crossbow_Giver : CustomInventory replaces Crossbow
{
	Game Heretic
	SpawnID 27
    Inventory.PickupMessage "$TXT_WPNCROSSBOW"
	Inventory.RespawnTics 105
	Inventory.PickupSound ""
    States
    {
    Spawn:
        WBOW A 1 A_SpawnItemEx("WeaponPickupChecker_Crossbow", 0, 0, 0, 0, 0, 0, 0, SXF_NOCHECKPOSITION, 0)
        WBOW AA 18
        Loop
    Pickup:
		TNT1 A 1 A_JumpIfInventory("Crossbow_Toby", 1, "HasCrossbow")
        TNT1 A 1 A_GiveInventory("Crossbow_Toby", 1)
		TNT1 A 1 A_PlaySoundEx("weapons/cbow_pkup", "Auto", 0, -1)
        Stop
	HasCrossbow:
		TNT1 A 1 A_JumpIfInventory("BagOfHolding_Toby", 1, "HasBP")
        TNT1 A 1 A_JumpIfInventory("CrossbowAmmo", 50, "NoPickup")
        TNT1 A 1 A_GiveInventory("CrossbowAmmo", 5)
		TNT1 A 1 A_PlaySoundEx("weapons/cbow_pkup", "Auto", 0, -1)
		TNT1 A 1 A_PlaySoundEx("ann/crossbow", "Auto", 0, -1)
		Stop
    HasBP:
        TNT1 A 1 A_JumpIfInventory("CrossbowAmmo", 100, "NoPickup")
        TNT1 A 1 A_GiveInventory("CrossbowAmmo", 5)
		TNT1 A 1 A_PlaySoundEx("weapons/cbow_pkup", "Auto", 0, -1)
		TNT1 A 1 A_PlaySoundEx("ann/crossbow", "Auto", 0, -1)
        Stop
    NoPickup:
        TNT1 A 1 A_PlaySoundEx("stat/full", "SoundSlot6", 0, 0)
        Fail
    }
}
